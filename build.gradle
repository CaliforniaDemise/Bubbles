plugins {
    id "com.gtnewhorizons.retrofuturagradle" version "1.4.1"
}

group = project.group
version = project.mod_version

base {
    archivesName = project.archive_name
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(8))
    }
}

minecraft {
    mcVersion = "1.12.2"
    extraRunJvmArguments.addAll("-Xms128m", "-Xmx2048m")
    injectedTags.put("MOD_VERSION", project.version)
}

apply from: "dependencies.gradle"

for (File at : sourceSets.main.resources.files) {
    if (at.name.toLowerCase().endsWith("_at.cfg")) {
        tasks.deobfuscateMergedJarToSrg.accessTransformerFiles.from(at)
        tasks.srgifyBinpatchedJar.accessTransformerFiles.from(at)
    }
}

processResources {
    inputs.property 'version', project.version

    filesMatching(['mcmod.info']) { fcd ->
        fcd.expand('version': project.version)
    }

    rename '(.+_at.cfg)', 'META-INF/$1' // Make sure Access Transformer files are in META-INF folder
}

jar {
    manifest {
        attributes(['FMLAT':'baubles_at.cfg'])
    }
}

tasks.injectTags.configure {
    outputClassName.set("baubles.common.Tags")
}

tasks.setupDecompWorkspace.dependsOn(tasks.injectTags)