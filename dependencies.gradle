repositories {
    maven {
        name 'CleanroomMC Maven' // HEI
        url 'https://maven.cleanroommc.com'
    }
    exclusiveContent {
        forRepository {
            maven {
                url "https://cursemaven.com"
            }
        }
        filter {
            includeGroup "curse.maven"
        }
    }
}

dependencies {
    var jei = "mezz:jei:4.25.5"
    var mt = "curse.maven:mouse-tweaks-unofficial-461660:4661407"
    var ut = "curse.maven:universaltweaks-705000:5529764"
    var ca = "curse.maven:cosmeticarmor-237307:2937869"
    var wings = "curse.maven:wings-302584:2829351"
    var aa = "curse.maven:actuallyadditions-228404:3117927"
    var qt = "curse.maven:quality-tools-264756:2732994"
    var botania = "curse.maven:botania-225643:3330934"
    var minieffects = "curse.maven:minieffects-1081085:6143104"
    var mixins = project.runtime_ut.toBoolean() || project.runtime_me.toBoolean()

    // JEI Extra Bounds
    if (project.runtime_jei.toBoolean()) implementation(jei)
    else compileOnly(jei)

    // Mouse Tweaks
    if (project.runtime_mt.toBoolean()) runtimeOnly(mt)

    // No Recipe Book
    if (project.runtime_ut.toBoolean()) {
        runtimeOnly("com.cleanroommc:configanytime:3.0")
        runtimeOnly(ut) { transitive = false }
    }
    compileOnly(ut) { transitive = false }

    // Cosmetic Armors
    if (project.runtime_ca.toBoolean()) runtimeOnly(ca)
    compileOnly(ca)

    // Wings
    if (project.runtime_wings.toBoolean()) runtimeOnly(wings)
    compileOnly(wings)

    // Compatibility
    if (project.runtime_aa.toBoolean()) runtimeOnly rfg.deobf(aa)
    compileOnly rfg.deobf(aa)

    // Fixes
    if (project.runtime_qt.toBoolean()) {
        runtimeOnly "curse.maven:potion-core-242872:2905184"
        implementation rfg.deobf(qt)
    }

    if (project.runtime_botania.toBoolean()) {
        runtimeOnly "curse.maven:autoreglib-250363:2746011"
        runtimeOnly rfg.deobf(botania)
    }
    compileOnly rfg.deobf(botania)

    if (project.runtime_rla.toBoolean()) implementation "curse.maven:rl-artifacts-819886:4475428"

    if (project.runtime_me.toBoolean()) runtimeOnly(minieffects) { transitive = false }
    compileOnly(minieffects) { transitive = false }

    compileOnly "curse.maven:cofh-core-69162:2920433"
    compileOnly "curse.maven:wb-684624:4203074"

    if (project.runtime_tab.toBoolean()) implementation "curse.maven:tab-279900:5717039"

    if (mixins) {
        annotationProcessor 'org.ow2.asm:asm-debug-all:5.2'
        annotationProcessor 'com.google.guava:guava:24.1.1-jre'
        annotationProcessor 'com.google.code.gson:gson:2.8.6'
        String mixinBooter = modUtils.enableMixins('zone.rong:mixinbooter:9.1')
        api (mixinBooter) { transitive = false }
        annotationProcessor (mixinBooter) { transitive = false }
    }
}