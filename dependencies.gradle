repositories {
    maven {
        name 'CleanroomMC Maven' // HEI
        url 'https://maven.cleanroommc.com'
    }
    exclusiveContent {
        forRepository {
            maven {
                url "https://curse.cleanroommc.com"
            }
        }
        filter {
            includeGroup "curse.maven"
        }
    }
}

dependencies {
    compileOnly "mezz:jei:4.25.5"
    compileOnly ("curse.maven:universaltweaks-705000:5529764") { transitive = false }
    compileOnly "curse.maven:cosmeticarmor-237307:2937869"
    compileOnly "curse.maven:wings-302584:2829351"
    compileOnly "curse.maven:botania-225643:3330934"
    compileOnly ("curse.maven:minieffects-1081085:6143104") { transitive = false }
    compileOnly ("com.cleanroommc:groovyscript:1.2.4") { transitive = false }

    compileOnly "curse.maven:cofh-core-69162:2920433"

    if (project.just_enough_items.toBoolean()) runtimeOnly "mezz:jei:4.25.5"
    if (project.mouse_tweaks.toInteger() != 0) {
        if (project.mouse_tweaks.toInteger() == 1) runtimeOnly "curse.maven:mouse-tweaks-60089:3359843"
        else runtimeOnly "curse.maven:mouse-tweaks-unofficial-461660:4661407"
    }
    if (project.universal_tweaks.toBoolean()) {
        runtimeOnly "com.cleanroommc:configanytime:3.0"
        runtimeOnly ("curse.maven:universaltweaks-705000:5529764") { transitive = false }
    }
    if (project.cosmetic_armor.toBoolean()) runtimeOnly "curse.maven:cosmeticarmor-237307:2937869"
    if (project.wings.toBoolean()) {
        runtimeOnly "curse.maven:llibrary-243298:3116493"
        runtimeOnly "curse.maven:wings-302584:2829351"
    }
    if (project.actually_additions.toBoolean()) {
        runtimeOnly "curse.maven:actuallyadditions-228404:3117927"
    }
    if (project.quality_tools.toBoolean()) {
        runtimeOnly "curse.maven:potion-core-242872:2905184"
        runtimeOnly "curse.maven:quality-tools-264756:2732994"
    }
    if (project.botania.toBoolean()) {
        runtimeOnly "curse.maven:autoreglib-250363:2746011"
        runtimeOnly "curse.maven:botania-225643:3330934"
    }
    if (project.artifacts_mod.toInteger() != 0) {
        if (project.artifacts_mod.toInteger() == 1) runtimeOnly "curse.maven:artifacts-312353:4871204"
        else runtimeOnly "curse.maven:rlartifacts-819886:4475428"
    }
    if (project.mini_effects.toBoolean()) {
        runtimeOnly ("curse.maven:minieffects-1081085:6143104") { transitive = false }
    }
    if (project.trinkets_and_baubles.toBoolean()) {
        runtimeOnly "curse.maven:tab-279900:5717039"
    }
    if (project.groovyscript.toBoolean()) {
        runtimeOnly ("com.cleanroommc:groovyscript:1.2.4") { transitive = false }
    }

    var mixins = project.universal_tweaks.toBoolean() || project.mini_effects.toBoolean() || project.groovyscript.toBoolean()
    if (mixins) {
        annotationProcessor 'org.ow2.asm:asm-debug-all:5.2'
        annotationProcessor 'com.google.guava:guava:24.1.1-jre'
        annotationProcessor 'com.google.code.gson:gson:2.8.6'
        String mixinBooter = modUtils.enableMixins('zone.rong:mixinbooter:9.1')
        api (mixinBooter) { transitive = false }
        annotationProcessor (mixinBooter) { transitive = false }
    }
}